buildscript {
    ext.kotlin_version = '1.3.31'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    if (!project.ext.has('pubName'))
        project.ext.set('pubName', false)
    if (!project.ext.has('pubSources'))
        project.ext.set('pubSources', true)
    if (!project.ext.has('pubComponent'))
        project.ext.set('pubComponent', null)
}

rootProject.ext.set('pubVersion', '0.1-beta10')
rootProject.ext.set('groupId', 'com.spreedly')
rootProject.ext.set('groupId', 'com.spreedly')

rootProject.buildDir = 'build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

task clean_all(type: Delete) {
    delete rootProject.buildDir
}

gradle.projectsEvaluated {
    def exportedProjects = [
            ":core-sdk",
    ]
    task alljavadoc(type: Javadoc) {
        source exportedProjects.collect { project(it).sourceSets.main.allJava }
        classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
        destinationDir = file("docs")
    }

    subprojects {
        if (project.pubName) {
            print("adding pub info ${project.pubName}")
            if (project.plugins.hasPlugin('android') && project.pubSources) {
                task generateSourcesJar(type: Jar) {
                    archiveClassifier.set('sources')
                    from android.sourceSets.main.java.srcDirs
                }

                artifacts {
                    archives sourcesJar
                }
            }

            publishing {
                publications {
                    maven(MavenPublication) {
                        groupId = rootProject.groupId
                        artifactId = project.pubName
                        version = rootProject.pubVersion

                        from(project.pubComponent ?: components.findByName('release') ?: components.findByName('java'))
                    }
                }

                repositories {
                    maven {
                        // change URLs to point to your repos, e.g. http://my.org/repo
                        def releasesRepoUrl = "${rootProject.buildDir}/repos/releases"
                        def snapshotsRepoUrl = "${rootProject.buildDir}/repos/snapshots"
                        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    }

                    maven {
                        name = "github"
                        url = uri("https://maven.pkg.github.com/spreedly/mobile-sdk-android")
                        credentials {
                            def properties = new Properties()
                            properties.load(project.rootProject.file('local.properties').newDataInputStream())

                            username = properties.getProperty("gpr.user") ?: System.getenv("USERNAME")
                            password = properties.getProperty("gpr.key") ?: System.getenv("TOKEN")
                        }
                    }

                    maven {
                        name = "branch"
                        url = "${rootProject.buildDir}/maven"
                    }
                }
            }
        }
    }
}

task cleanupMaven(type:Delete) {
    delete "${rootProject.buildDir}/maven/"
    followSymlinks = true

    doFirst {
        def buildDir = new File("${rootProject.buildDir}")
        if (!buildDir.exists())
            buildDir.mkdirs()
    }
}

task cloneMaven(type:Exec, dependsOn:[cleanupMaven]) {
    commandLine 'sh', '-c', "cd ${rootProject.buildDir}; git clone https://github.com/spreedly/mobile-sdk-android.git --no-checkout maven"
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task checkoutMavenBranch(type:Exec, dependsOn: [cloneMaven]) {
    commandLine 'sh', '-c', "cd ${rootProject.buildDir}/maven; git checkout maven"
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task addMavenBranch(type:Exec) {
    commandLine 'sh', '-c', "cd ${rootProject.buildDir}/maven; git add ."
}

task commitMavenBranch(type:Exec, dependsOn: [addMavenBranch]) {
    commandLine 'sh', '-c', "cd ${rootProject.buildDir}/maven; git commit -m maven"
}

task pushMavenBranch(type:Exec, dependsOn: [commitMavenBranch]) {
    commandLine 'sh', '-c', "cd ${rootProject.buildDir}/maven; git push"
}

task doPub(type: GradleBuild, dependsOn: checkoutMavenBranch) {
    tasks = ['publishAllPublicationsToBranchRepository']
}
addMavenBranch.mustRunAfter doPub

task release(type: GradleBuild) {
    tasks = ['checkoutMavenBranch', 'doPub', 'pushMavenBranch']
}

task environment(type: Exec) {
    //environment 'PATH', "${environment.PATH}:/bin"
    commandLine 'sh'
}